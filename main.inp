set verbose off

clear

include yahoo_get
include "help_functions.inp"
include "patterns.inp"

open "dj.csv"

strings stocks = array($nobs)

# Build stocks array
loop i=1..$nobs
    stocks[i] = StockSymbol[i]
endloop

clear --dataset

# Calculate days from 1950-1-1 to 2022-7-31 and build dataset
epochStart = epochday(1950,01,01)
epochEnd = epochday(2022,07,31)
days = dayspan(epochStart, epochEnd, 5)

nulldata days --preserve
setobs 5 "1950-01-01"
series epoch = epochday($obsdate)

bundle settings = build_bundle_settings()
function_names = getkeys(settings.patterns)

stock_sym = stocks[1]

# Perpare result file
path_results = $workdir ~ sprintf("%s_%s.csv","\results\results_stocks",strftime($now[1], "%m%d_%H%M"))
path_stocks_inteval = $workdir ~ sprintf("%s_%s.csv","\results\stocks_interval",strftime($now[1], "%m%d_%H%M"))

outfile "@path_results" --quiet
    print_first_line()
end outfile

#Prepare interval file
outfile "@path_stocks_inteval" --quiet
    printf("Symbol,Start,Stop\n")
end outfile

set stopwatch 

loop n_stock = 1 .. nelem(stocks)
    stock_sym = stocks[n_stock]
    list Lget = yahoo_full("@stock_sym")

    if nelem(Lget) 
        list L = Lget[2] Lget[3] Lget[4] Lget[5]
        smpl L --no-missing

        outfile "@path_stocks_inteval" --quiet --append
            printf("%s,%s,%s\n", stock_sym, isodate(epoch[1],1), isodate(epoch[$nobs],1))
        end outfile
        
        outMatrix = {}

        trdR_bigger = 0
        qntR_bigger = 0

        loop trdR = 1 .. nelem(settings.trend_ranges)
            if settings.trend_ranges[trdR] > $nobs
                trdR_bigger = 1
            endif

            loop qntR = 1 .. nelem(settings.quantile_ranges) 
                if settings.quantile_ranges[qntR] > $nobs
                    qntR_bigger = 1
                endif

                bundle b = null
                
                b.trend_range = trdR_bigger ? $nobs : settings.trend_ranges[trdR]
                b.quantile_range = qntR_bigger ? $nobs : settings.quantile_ranges[qntR]
                init(&b, L)
                
                loop retD = 1 .. nelem(settings.return_days)
                    loop foreach funN function_names
                        series temp_series = feval("$funN", b) 
                        loop i = 1 .. $nobs
                            if ok(temp_series[i]) && temp_series[i] == 1
                                bullish = settings.patterns.$funN.trend
                                matrix M = get_return(b,i,settings.return_days[retD],bullish)
                                M = {epoch[i],settings.patterns["$funN"].id,trdR,qntR,retD} ~ M
                                outMatrix = outMatrix | M
                            endif
                        endloop
                        flush
                    endloop
                endloop
            if qntR_bigger
                break
            endif
            endloop
        if trdR_bigger
            break
        endif
        endloop

        outfile "@path_results" --quiet --append
            print_matrix(outMatrix, stock_sym)
        end outfile

    endif

    delete L --force
    delete Lget --force
    delete temp_series --force

    # Print for flush
    printf("Done %g of %g\n",n_stock,nelem(stocks))

    smpl full

endloop

e = $stopwatch
print e